buildscript {
    ext.kotlin_version = '1.2.30'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "com.jfrog.bintray" version "1.7"
}

apply plugin: 'kotlin'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

def libGitUrl = gitUrl;
def libWebUrl = webUrl;
def libDescription = description;
def libGroupId = groupId;
def libArtifactId = artifactId;
def libVersion = version;
def libLicenseUrl = licenseUrl;

group libGroupId
version libVersion

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    // Unit testing dependencies
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile 'org.assertj:assertj-core:3.8.0'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

install {
    repositories.mavenInstaller {
        pom.project {
            name libArtifactId
            description libDescription
            url libWebUrl
            inceptionYear '2018'

            packaging 'jar'
            groupId libGroupId
            artifactId libArtifactId
            version libVersion

            licenses {
                license {
                    name 'Apache'
                    url libLicenseUrl
                }
            }
        }
    }
}

bintray {
    user = bintray_user
    key = bintray_apikey
    pkg {
        repo = groupId
        name = artifactId
        licenses = ['Apache']
        vcsUrl = gitUrl
        version {
            name = version
        }
    }
    configurations = ['archives']
}